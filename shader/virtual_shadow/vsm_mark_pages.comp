#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_samplerless_texture_functions : enable
#extension GL_KHR_memory_scope_semantics : enable

// #define MARK_PAGES 1
// #define HIZ_PAGES 1
// #define COMPOSE 1
// #define DEBUG 1
#define LWC 1

#include "virtual_shadow/vsm_common.glsl"
#include "lighting/tonemapping.glsl"
#include "scene.glsl"
#include "common.glsl"

#if defined(GL_COMPUTE_SHADER)
//layout(local_size_x = 8, local_size_y = 8) in;
layout(local_size_x = 16, local_size_y = 8) in;
#endif

layout(std140, push_constant) uniform Push {
  float vsmMipBias;
  };
layout(binding  = 0, std140) uniform UboScene {
  SceneDesc scene;
  };
layout(binding = 1)        uniform texture2D  gbufDiffuse;
layout(binding = 2)        uniform utexture2D gbufNormal;
layout(binding = 3)        uniform texture2D  depth;
layout(binding = 4, r32ui) uniform uimage3D   pageTbl;
layout(binding = 5, r32ui) uniform uimage3D   pageTblDepth;

ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);

shared uint pageHiZ[6];

uint pack565_16(ivec3 a, uint b) {
  uint x = (a.x & 0x1F)   << 0;
  uint y = (a.y & 0x3F)   << 5;
  uint z = (a.z & 0x1F)   << 11;
  uint w = (  b & 0xFFFF) << 16;
  //uint d = uint(z*0xFFFF) << 16;
  return x | y | z | w;
  }

uvec4 unpack565_16(uint p) {
  uvec4 ret;
  ret.x = (p >>  0) & 0x1F;
  ret.y = (p >>  5) & 0x3F;
  ret.z = (p >> 11) & 0x1F;
  ret.w = (p >> 16);
  return ret;
  }

void insertHiZ(ivec3 at, float z) {
  if(z>=1)
    return;
  //imageAtomicStore(pageTbl, at, 1u, gl_ScopeDevice, gl_StorageSemanticsNone, gl_SemanticsRelaxed);
  //return;

  uint iz  = uint(z*0xFFFF);
  uint cur = pack565_16(at,iz);
  for(uint i=0; i<pageHiZ.length(); ++i) {
    uint v = atomicMin(pageHiZ[i], cur);
    if(v==0xFFFFFFFF || (v&0xFFFF)==(cur&0xFFFF))
      return;
    cur = (v > cur) ? v : cur;
    }
  // imageAtomicAdd(pageTbl, ivec3(0), 1u); //counter
  imageAtomicStore(pageTbl, at, 1u, gl_ScopeDevice, gl_StorageSemanticsNone, gl_SemanticsRelaxed);
  imageAtomicMin(pageTblDepth, at, iz);
  }

void flushHiZ() {
  const uint lane = gl_LocalInvocationIndex;
  if(lane >= pageHiZ.length())
    return;
  const uint v = pageHiZ[lane];
  if(v==0xFFFFFFFF)
    return;

  uvec4 dx = unpack565_16(v);
  ivec3 at = ivec3(dx.xyz);
  uint  iz = dx.w;

  //NOTE: require vulkan memory-model extension or vulkan 1.2
  //imageAtomicStore(pageTbl, at, 1u, gl_ScopeDevice, gl_StorageSemanticsNone, gl_SemanticsRelaxed);
  //imageStore(pageTbl, at, uvec4(1u));

  imageAtomicExchange(pageTbl, at, 1u);
  imageAtomicMin(pageTblDepth, at, iz);
  }

vec4 worldPos(ivec2 frag, float depth) {
  const vec2 fragCoord = ((frag.xy+0.5)*scene.screenResInv)*2.0 - vec2(1.0);
  const vec4 scr       = vec4(fragCoord.x, fragCoord.y, depth, 1.0);
#if defined(LWC)
  return scene.viewProjectLwcInv * scr;
#else
  return scene.viewProjectInv * scr;
#endif
  }

vec3 shadowPos(float z, ivec2 offset) {
  const vec4  wpos = worldPos(pixelPos+offset, z);
#if defined(LWC)
  vec4 shPos = scene.viewVirtualShadowLwc * wpos;
#else
  vec4 shPos = scene.viewVirtualShadow * wpos;
#endif
  shPos.xyz /= shPos.w;
  return shPos.xyz;
  }

int shadowLod(vec2 dx, vec2 dy) {
  float px     = dot(dx, dx);
  float py     = dot(dy, dy);
  float maxLod = 0.5 * log2(max(px, py)); // log2(sqrt()) = 0.5*log2()
  float minLod = 0.5 * log2(min(px, py));

  const float bias = vsmMipBias;
  //return max(0, int((minLod + maxLod)*0.5 + bias + 0.5));
  return max(0, int(minLod + bias + 0.5));
  }

void markPage(ivec2 pageI, int mip, float depth) {
#if defined(MARK_PAGES)
  insertHiZ(ivec3(pageI, mip), depth);
#endif
  }

void realMain() {
  const ivec2 size = textureSize(depth, 0);
  if(any(greaterThanEqual(pixelPos, size)))
    return;

  const float z = texelFetch(depth, pixelPos, 0).x;
  if(z==1)
    return;

  const vec3 normal = normalFetch(gbufNormal, pixelPos);
  if(dot(scene.sunDir,normal)<=0)
    return;

  vec3  shPos0 = shadowPos(z, ivec2(0,0));
  vec2  shPos1 = shadowPos(z, ivec2(1,0)).xy;
  vec2  shPos2 = shadowPos(z, ivec2(0,1)).xy;

  // expected to have ~9 mips
  int   mip    = shadowLod((shPos1 - shPos0.xy)*VSM_CLIPMAP_SIZE,
                           (shPos2 - shPos0.xy)*VSM_CLIPMAP_SIZE);
  vec2  page   = shPos0.xy / (1<<mip);
  while(abs(page.x)>=1 || abs(page.y)>=1) {
    page /= 2;
    mip++;
    }
  if(any(greaterThan(abs(page), vec2(1))) || mip>=VSM_PAGE_MIPS)
    return;

  ivec2 pageI = ivec2((page*0.5+0.5)*VSM_PAGE_TBL_SIZE);
  markPage(pageI, mip, shPos0.z);
  }

void main() {
#if defined(GL_COMPUTE_SHADER)
  const uint lane = gl_LocalInvocationIndex;
  if(lane < pageHiZ.length())
    pageHiZ[lane] = 0xFFFFFFFF;
  barrier();
#endif

  realMain();
#if defined(GL_COMPUTE_SHADER)
  barrier();
  flushHiZ();
#endif
  }
