#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_samplerless_texture_functions : enable

#include "common.glsl"
#include "scene.glsl"
#include "rtsm/rtsm_common.glsl"

//#define LARGE 1

layout(local_size_x = RTSM_BIN_SIZE, local_size_y = RTSM_BIN_SIZE) in;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;
const uint MaxVert    = 64;
const uint MaxPrim    = 64;

layout(binding = 0, r8)     uniform  image2D    outputImage;
layout(binding = 1, std140) uniform  UboScene   { SceneDesc scene; };
layout(binding = 2)         uniform  utexture2D gbufNormal;
layout(binding = 3)         uniform  texture2D  depth;
layout(binding = 4)         uniform  utexture2D smallTiles;
layout(binding = 5, std430)          buffer Pos { uint alloc; uint  data[]; } pos;
layout(binding = 6, rg32ui) uniform  uimage2D   primitiveBins;
layout(binding = 7, r32ui)  uniform  uimage2D   dbgImage;


vec3  dbgColor  = vec3(0);
ivec2 fragCoord = ivec2(gl_GlobalInvocationID.xy);

// counter
uint numMeshletsAll = 0;

//
shared uvec4 rayTileBbox[MaxSlices];
shared uint  rayTileMinDepth[MaxSlices];
shared uint  raySliceBits;
shared uint  numSlices;

// visible meshlets
shared uint numPrimitives;
shared uint primitives[4*1024];

shared uint primListPtr;

uvec2 pullMeshList() {
  return texelFetch(smallTiles, ivec2(fragCoord/RTSM_SMALL_TILE), 0).xy;
  }

shared uint dbgCounter;
uint mergeCounter(uint cnt) {
  dbgCounter = 0;
  barrier();

  atomicAdd(dbgCounter, cnt);
  barrier();

  return dbgCounter;
  }

// utility
uint depthSlice(const float z) {
  float dZ   = linearDepth(z,      scene.clipInfo);
  float d0   = linearDepth(0,      scene.clipInfo);
  float d1   = linearDepth(0.9999, scene.clipInfo);
  float d    = (dZ-d0)/(d1-d0);

  return min(uint(d*MaxSlices+0.5), MaxSlices);
  }

uint packPrimitiveId(uint meshlet, uint prim) {
  return (meshlet << 8) | prim;
  }

// light/occlusion/matrix
vec4 worldPos(ivec2 frag, float depth) {
  const vec2 fragCoord = ((frag.xy+0.5)*scene.screenResInv)*2.0 - vec2(1.0);
  const vec4 scr       = vec4(fragCoord.x, fragCoord.y, depth, 1.0);
  return scene.viewProjectLwcInv * scr;
  }

// ray managment
vec3 calcRayOrigin(const vec4 wpos) {
  vec4 shPos = scene.viewVirtualShadowLwc * wpos;
  shPos.xyz /= shPos.w;
  return shPos.xyz;
  }

// meshlets-trace
bool bboxIncludes(vec4 aabb, vec2 p) {
  if(aabb.x < p.x && p.x < aabb.z &&
     aabb.y < p.y && p.y < aabb.w)
    return true;
  return false;
  }

bool isAabbVisible(const vec4 aabb, const float depthMax) {
  for(uint i=0; i<numSlices; ++i) {
    vec4 rbb; float rayd;
    rbb.x = uintBitsToFloat(rayTileBbox[i].x);
    rbb.y = uintBitsToFloat(rayTileBbox[i].y);
    rbb.z = uintBitsToFloat(rayTileBbox[i].z);
    rbb.w = uintBitsToFloat(rayTileBbox[i].w);
    rayd  = uintBitsToFloat(rayTileMinDepth[i]);
    if(rayd > depthMax)
      ;//continue;
    if(!bboxIntersect(aabb, rbb))
      continue;
    return true;
    }
  return false;
  }

// primitive-trace
uvec3 pullPrimitive(const uint ptr, const uint laneId) {
  uint bits = pos.data[ptr+laneId];

  uvec3 prim;
  prim.x = ((bits >>  0) & 0xFF);
  prim.y = ((bits >>  8) & 0xFF);
  prim.z = ((bits >> 16) & 0xFF);
  return prim;
  }

vec3 pullVertex(uint ptr, const uint laneId) {
  ptr += 4*laneId;

  vec3 ret;
  ret.x = uintBitsToFloat(pos.data[ptr+0]);
  ret.y = uintBitsToFloat(pos.data[ptr+1]);
  ret.z = uintBitsToFloat(pos.data[ptr+2]);
  return ret;
  }

void processMeshletPrimitives(const uint ptr, const uint primCount, const uint laneID) {
  const uint maxMesh = 6;

  if(laneID<primCount) {
    uint ptrM = ptr;

    ptrM += maxMesh;
    const uvec3 tri = pullPrimitive(ptrM, laneID);

    ptrM += primCount;
    const vec3  a   = pullVertex(ptrM, tri.x);
    const vec3  b   = pullVertex(ptrM, tri.y);
    const vec3  c   = pullVertex(ptrM, tri.z);
    const vec4  tbb = bboxOf(a.xy, b.xy, c.xy);
    const float d   = max(a.x, max(b.z, c.z));

    if(!isAabbVisible(tbb, d))
      return;

    const uint id = atomicAdd(numPrimitives, 1);
    if(id<primitives.length())
      primitives[id] = packPrimitiveId(ptr, laneID);
    }
  }

void traceTile() {
  const uint  laneID      = gl_LocalInvocationIndex;

  const uvec2 meshListSrc = pullMeshList();
  const uint  tilePtr     = meshListSrc.x;
  const uint  visCount    = meshListSrc.y;
  const uint  meshPerStep = NumThreads / MaxPrim;
  const uint  steps       = (visCount + meshPerStep - 1)/meshPerStep;

  for(uint s=0; s<steps; ++s) {
    const uint i   = laneID/MaxPrim + s*meshPerStep;
    const uint ptr = i<visCount ? pos.data[tilePtr + i].x : NULL;

    if(ptr!=NULL) {
      const uint laneMs  = gl_LocalInvocationIndex%MaxPrim;
      const uint primCnt = unpackPrimitiveCount(pos.data[ptr+5]);
      processMeshletPrimitives(ptr, primCnt, laneMs);
      }
    }
  }

void storeResultPrim() {
  const uint  laneID    = gl_LocalInvocationIndex;

  if(gl_LocalInvocationIndex==0) {
    const uint n      = min(numPrimitives, primitives.length()) & 0xFFFF;
    const uint badBit = (numPrimitives>primitives.length()) ? (1u << 31) : 0;
    if(numPrimitives>primitives.length())
      numPrimitives = 0;

    primListPtr = numPrimitives==0 ? NULL : atomicAdd(pos.alloc, numPrimitives);
    imageStore(primitiveBins, ivec2(gl_WorkGroupID.xy), uvec4(primListPtr, n | badBit,0,0));
    }
  barrier();

  for(uint i=laneID; i<numPrimitives; i+=NumThreads)
    pos.data[primListPtr + i] = primitives[i];
  }

void processFragment(const ivec2 fragCoord) {
  const ivec2 size   = textureSize(depth, 0);
  const bool  valid  = all(lessThan(fragCoord, size));
  if(!valid)
    return;

  const float lamb   = imageLoad(outputImage, fragCoord).x;
  const bool  actRay = !(lamb==0);
  if(!actRay)
    return;

  const float z = texelFetch(depth, fragCoord, 0).x;
  if(z==1.0)
    return;

  const uint  slice = depthSlice(z);
  const vec4  wpos  = worldPos(fragCoord.xy, z);
  const vec3  ray   = calcRayOrigin(wpos);

  atomicOr(raySliceBits, 1u << slice);
  atomicMin(rayTileBbox[slice].x,   floatToOrderedUint(ray.x));
  atomicMin(rayTileBbox[slice].y,   floatToOrderedUint(ray.y));
  atomicMax(rayTileBbox[slice].z,   floatToOrderedUint(ray.x));
  atomicMax(rayTileBbox[slice].w,   floatToOrderedUint(ray.y));
  atomicMin(rayTileMinDepth[slice], floatToOrderedUint(ray.z));
  }

void tileBbox() {
  const uint laneID = gl_LocalInvocationIndex;

  processFragment(fragCoord);
  barrier();

  vec4 rbb; float rayD;
  if(laneID<rayTileBbox.length()) {
    rbb.x = orderedUintToFloat(rayTileBbox[laneID].x);
    rbb.y = orderedUintToFloat(rayTileBbox[laneID].y);
    rbb.z = orderedUintToFloat(rayTileBbox[laneID].z);
    rbb.w = orderedUintToFloat(rayTileBbox[laneID].w);
    rayD  = orderedUintToFloat(rayTileMinDepth[laneID]);
    }
  barrier();

  if(laneID<rayTileBbox.length() && (raySliceBits & (1u << laneID))!=0) {
    const uint i = bitCount(raySliceBits & ((1u << laneID)-1u));

    rayTileBbox[i].x   = floatBitsToUint(rbb.x);
    rayTileBbox[i].y   = floatBitsToUint(rbb.y);
    rayTileBbox[i].z   = floatBitsToUint(rbb.z);
    rayTileBbox[i].w   = floatBitsToUint(rbb.w);
    rayTileMinDepth[i] = floatBitsToUint(rayD);
    }
  numSlices = bitCount(raySliceBits);
  }

void main() {
  const uint  laneID = gl_LocalInvocationIndex;
  const ivec2 size   = textureSize(depth, 0);
  const bool  valid  = all(lessThan(fragCoord, size));

  numPrimitives  = 0;
  numMeshletsAll = 0;
  raySliceBits   = 0;
  if(laneID<rayTileBbox.length()) {
    rayTileBbox[laneID]     = uvec4(0xFFFFFFFF, 0xFFFFFFFF, 0, 0);
    rayTileMinDepth[laneID] = 0xFFFFFFFF;
    }
  barrier();

  tileBbox();
  barrier();

  traceTile();
  barrier();

  storeResultPrim();

  //const uint ms = mergeCounter(numMeshletsAll);
  if(valid) {
    imageStore(dbgImage, fragCoord, uvec4(numPrimitives));
    //imageStore(dbgImage, fragCoord, uvec4(ms));
    }
  }

