#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_samplerless_texture_functions : enable
// bindless
#extension GL_EXT_nonuniform_qualifier : enable
// #extension GL_KHR_memory_scope_semantics : enable

#include "common.glsl"
#include "scene.glsl"
#include "rtsm/rtsm_common.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;
const uint MaxVert    = 64;
const uint MaxPrim    = 64;
const uint MaxInd     = (MaxPrim*3);

struct Vertex {
  vec3 pos;
  };

layout(push_constant, std430) uniform UboPush {
  int meshletCount;
  } push;
layout(binding = 0, r8)     uniform  image2D    outputImage;
layout(binding = 1, std140) uniform  UboScene   { SceneDesc scene; };
layout(binding = 2)         uniform  utexture2D gbufNormal;
layout(binding = 3)         uniform  texture2D  depth;
layout(binding = 4, r32ui)  uniform  uimage2D   primitiveBins;
layout(binding = 5, std430) readonly buffer Pos { uint alloc; uint data[]; } pos;
layout(binding = 6, r32ui)  uniform  uimage2D   dbgImage;

layout(binding = 7)         uniform  texture2D  textureMain[];
layout(binding = 8)         uniform  sampler    samplerMain;


vec3  dbgColor  = vec3(0);
ivec2 fragCoord = ivec2(gl_GlobalInvocationID.xy);
ivec2 tileCoord = (fragCoord/RTSM_BIN_SIZE)*RTSM_BIN_SIZE;

// counters
uint numMeshlets  = 0;
uint numTriangles = 0;

shared uint dbgCounter;
uint mergeCounter(uint cnt) {
  dbgCounter = 0;
  barrier();

  atomicAdd(dbgCounter, cnt);
  barrier();

  return dbgCounter;
  }

// utility
shared uvec4 rayBbox[RTSM_PAGE_MIPS];
shared uint  rayZmin[RTSM_PAGE_MIPS];
uint         numRbb = 0;
void rayBboxses(uint pkgMask, const uint selfMask, const vec3 ray, bool activeRay) {
  uint i = 0;
  while(pkgMask!=0) {
    const int bit = findLSB(pkgMask);
    pkgMask &= ~(1u << bit);

    rayBbox[i] = uvec4(0xFFFFFFFF, 0xFFFFFFFF, 0, 0);
    rayZmin[i] = 0xFFFFFFFF;
    barrier();

    const bool isInBin = (selfMask & (1u << bit))!=0;
    if(activeRay && isInBin) {
      atomicMin(rayBbox[i].x, floatToOrderedUint(ray.x));
      atomicMin(rayBbox[i].y, floatToOrderedUint(ray.y));
      atomicMax(rayBbox[i].z, floatToOrderedUint(ray.x));
      atomicMax(rayBbox[i].w, floatToOrderedUint(ray.y));
      atomicMin(rayZmin[i],   floatToOrderedUint(ray.z));
      }
    barrier();
    if(rayBbox[i].x!=0xFFFFFFFF) {
      ++i;
      }
    }
  numRbb = i;
  }

// light/occlusion/matrix
float lambert(vec3 normal) {
  return max(0.0, dot(scene.sunDir,normal));
  }

vec4 worldPos(ivec2 frag, float depth) {
  const vec2 fragCoord = ((frag.xy+0.5)*scene.screenResInv)*2.0 - vec2(1.0);
  const vec4 scr       = vec4(fragCoord.x, fragCoord.y, depth, 1.0);
  return scene.viewProjectLwcInv * scr;
  // return scene.viewProjectInv * scr;
  }

// pagging
int shadowLod(vec2 dx, vec2 dy) {
  float px     = dot(dx, dx);
  float py     = dot(dy, dy);
  float maxLod = 0.5 * log2(max(px, py)); // log2(sqrt()) = 0.5*log2()
  float minLod = 0.5 * log2(min(px, py));

  const float bias = 0;
  return max(0, int(minLod + bias));
  }

int calcMipIndex(in vec2 shPos) {
  float d  = max(abs(shPos.x), abs(shPos.y));
  uint  id = uint(d);
  return findMSB(id)+1;
  }

// ray managment
vec3 calcRayOrigin(const vec4 wpos) {
  vec4 shPos = scene.viewVirtualShadowLwc * wpos;
  shPos.xyz /= shPos.w;
  return shPos.xyz;
  }

uint rayPacketId(const vec2 shPos, const float z) {
#if 0
  float dZ   = linearDepth(     z, scene.clipInfo);
  float d0   = linearDepth(0,      scene.clipInfo);
  float d1   = linearDepth(0.9999, scene.clipInfo);
  float d    = (dZ-d0)/(d1-d0);

  int id = int(d*RTSM_PAGE_MIPS);
  if(id>=RTSM_PAGE_MIPS)
    return 0;
  return 1u << id;
#else
  int mip = calcMipIndex(shPos);
  if(mip>=RTSM_PAGE_MIPS)
    return 0;
  return 1u << mip;
#endif
  }

shared uint rayPacketMask;
uint mergeMasks(uint m) {
  rayPacketMask = 0;
  barrier();
  atomicOr(rayPacketMask, m);
  barrier();
  return rayPacketMask;
  }

// meshlet-data
uvec3 pullPrimitive(const uint ptr, const uint laneId) {
  uint bits = pos.data[ptr+laneId];

  uvec3 prim;
  prim.x = ((bits >>  0) & 0xFF);
  prim.y = ((bits >>  8) & 0xFF);
  prim.z = ((bits >> 16) & 0xFF);
  return prim;
  }

Vertex pullVertex(uint ptr, const uint laneId) {
  ptr += 4*laneId;

  Vertex ret;
  ret.pos.x = uintBitsToFloat(pos.data[ptr+0]);
  ret.pos.y = uintBitsToFloat(pos.data[ptr+1]);
  ret.pos.z = uintBitsToFloat(pos.data[ptr+2]);
  //TODO: UV

  return ret;
  }

shared uint   maxPrim;
shared uvec3  primitiveIndices[MaxPrim];
shared float  primitiveAreaInv[MaxPrim];
shared Vertex meshVertices    [MaxVert];
bool isPrimitiveVisible(const vec4 aabb) {
  //float depthMax;

  for(uint i=0; i<numRbb; ++i) {
    const vec4 rbbox = orderedUintToFloat(rayBbox[i]);
    // if(uintBitsToFloat(rayZmin[i]) > depthMax)
    //   continue;
    if(!bboxIntersect(aabb, rbbox))
      continue;
    return true;
    }
  return false;
  }

void processMeshletPrimitives(uint ptr, uint primCount) {
  const uint laneID = gl_LocalInvocationIndex;

  if(laneID<primCount) {
    const uvec3 tri = pullPrimitive(ptr, laneID);
    const vec3  a   = meshVertices[tri.x].pos.xyz;
    const vec3  b   = meshVertices[tri.y].pos.xyz;
    const vec3  c   = meshVertices[tri.z].pos.xyz;

    const vec4 tbb = bboxOf(a.xy, b.xy, c.xy);
    if(!isPrimitiveVisible(tbb))
      return;

    const uint  id   = atomicAdd(maxPrim, 1);
    const float area = edgeFunction(a.xy, b.xy, c.xy);

    primitiveIndices[id] = tri;
    primitiveAreaInv[id] = (1.0/area);

    ++numTriangles;
    }
  }

void processMeshletVertices(const uint ptr) {
  const uint laneID = gl_LocalInvocationIndex;

  const uint vertCount = MaxVert; //TODO
  if(laneID<vertCount) {
    meshVertices[laneID] = pullVertex(ptr, laneID);
    }
  }

bool rasterizeMeshletShadow(const vec3 ray) {
  for(uint i=0; i<maxPrim; ++i) {
    uvec3 tri = primitiveIndices[i];
    vec3  a   = meshVertices[tri.x].pos.xyz;
    vec3  b   = meshVertices[tri.y].pos.xyz;
    vec3  c   = meshVertices[tri.z].pos.xyz;

    float w0  = edgeFunction(b.xy, c.xy, ray.xy);
    float w1  = edgeFunction(c.xy, a.xy, ray.xy);
    float w2  = edgeFunction(a.xy, b.xy, ray.xy);

    if(w0 < 0 || w1 < 0 || w2 < 0)
      continue;

    const float areaInv = primitiveAreaInv[i];
    const vec3  bc      = vec3(w0,w1,w2);
    const float depth   = dot(vec3(a.z,b.z,c.z), bc)*areaInv;
    if(depth > ray.z)
      return true;
    }
  return false;
  }

// meshlets-trace
shared uint numVisMeshlets;
shared uint meshletsId[NumThreads];

bool isMeshletVisible(uint ptr) {
  vec4  aabb;
  float depthMax;

  aabb.x   = uintBitsToFloat(pos.data[ptr+0]);
  aabb.y   = uintBitsToFloat(pos.data[ptr+1]);
  aabb.z   = uintBitsToFloat(pos.data[ptr+2]);
  aabb.w   = uintBitsToFloat(pos.data[ptr+3]);
  depthMax = uintBitsToFloat(pos.data[ptr+4]);

  for(uint i=0; i<numRbb; ++i) {
    const vec4 rbbox = orderedUintToFloat(rayBbox[i]);
    if(uintBitsToFloat(rayZmin[i]) > depthMax)
      continue;
    if(!bboxIntersect(aabb, rbbox))
      continue;
    return true;
    }
  return false;
  }

void pullMeshlets(const uint step, uint numMesh) {
  const uint laneID = gl_LocalInvocationIndex;
  const uint visId  = step*NumThreads + laneID;
  if(visId>=numMesh)
    return;

  const uint bId = visId+1;
  const uint ptr = imageLoad(primitiveBins, tileCoord + ivec2(bId%RTSM_BIN_SIZE, bId/RTSM_BIN_SIZE)).x;

  if(!isMeshletVisible(ptr))
    return;

  const uint id = atomicAdd(numVisMeshlets, 1);
  meshletsId[id] = ptr;
  ++numMeshlets;
  }

bool tracePacket(const vec3 ray) {
  const uint laneID = gl_LocalInvocationIndex;

  bool shadow = false;

  const uint numMesh = imageLoad(primitiveBins, tileCoord).x;
  const uint steps   = ((numMesh + NumThreads - 1)/NumThreads);
  for(uint i=0; i<steps; ++i) {
    numVisMeshlets = 0;
    barrier();

    pullMeshlets(i, numMesh);
    barrier();
    // continue;

    for(uint r=0; r<numVisMeshlets; ++r) {
      const uint maxMesh = 6;
      const uint ptr     = meshletsId[r];
      const uint primCnt = pos.data[ptr+5];

      processMeshletVertices(ptr+maxMesh+primCnt);
      maxPrim = 0;
      barrier();

      processMeshletPrimitives(ptr+maxMesh, primCnt);
      barrier();

      shadow = shadow || rasterizeMeshletShadow(ray);
      barrier();
      }
    }
  barrier();
  return shadow;
  }

void main() {
  numMeshlets = 0;
  barrier();

  const ivec2 size   = textureSize(depth, 0);
  const bool  valid  = all(lessThan(fragCoord, size));

  const float z      = valid ? texelFetch(depth, fragCoord, 0).x  : 1.0;
  const vec3  normal = valid ? normalFetch(gbufNormal, fragCoord) : vec3(0);
  const float lamb   = imageLoad(outputImage, fragCoord).x;

  const vec4  wpos   = worldPos(fragCoord.xy, z) + vec4(normal*0.002, 0);
  const vec3  ray    = calcRayOrigin(wpos);
  const bool  actRay = (lamb>0);

  const uint  selfMask = actRay ? rayPacketId(ray.xy,z) : 0;
  uint        pkgMask  = mergeMasks(selfMask);

  rayBboxses(pkgMask, selfMask, ray, actRay);

  const bool shadow = tracePacket(ray);
  if(actRay && shadow)
    imageStore(outputImage, fragCoord, vec4(0));

  if(valid) {
    //imageStore(dbgImage, fragCoord, uvec4(numRbb));
    imageStore(dbgImage, fragCoord, uvec4(mergeCounter(numMeshlets)));
    //imageStore(dbgImage, fragCoord, uvec4(mergeCounter(numTriangles)));
    }
  }
