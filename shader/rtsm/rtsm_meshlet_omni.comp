#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_samplerless_texture_functions : enable

// #define PASS0
// #define PASS1

#include "rtsm/rtsm_common.glsl"
#include "scene.glsl"
#include "common.glsl"

layout(local_size_x = 512) in;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;

layout(binding = 0, std140) uniform  UboScene    { SceneDesc   scene;                    };
layout(binding = 1, std430) readonly buffer Lbo  { LightSource lights[];                 };
layout(binding = 2, std430)          buffer VId  { uvec4       count;   LightId data[];  } vLights;
// layout(binding = 3, std430)
// layout(binding = 4, std430)
layout(binding = 5, std430) readonly buffer Cbo  { Cluster     clusters[];               };
layout(binding = 6, std430)          buffer Pos  { uint        alloc;  uint  data[];     } pos;

#include "rtsm/rtsm_scratch.glsl"

shared uint meshletsId[2048];
shared uint numMeshlets;
shared uint meshListPtr;

bool isMeshletVisible(uint ptr, const LightSource lx) {
  const vec4  sphere = pullMeshSphere(ptr);
  const vec3  dv     = lx.pos - sphere.xyz;
  const float r      = lx.range + sphere.w;
  return dot(dv, dv) <= r*r;
  }

void traceMeshlets(uint listPtr, uint visCount, const LightSource lx) {
  const uint laneID = gl_LocalInvocationIndex;

  numMeshlets = 0;
  barrier();

  for(uint i=laneID; i<visCount; i+=NumThreads) {
    const uint ptr = pos.data[listPtr + i];
    if(!isMeshletVisible(ptr, lx))
      continue;

    const uint id = atomicAdd(numMeshlets, 1);
    if(id<meshletsId.length())
      meshletsId[id] = ptr;
    }
  }

void store(uint lightId) {
  const uint laneID = gl_LocalInvocationIndex;
  if(laneID==0)
    meshListPtr = allocScratch(numMeshlets+1);
  barrier();

  if(meshListPtr==NULL) {
    vLights.data[lightId].numMeshlets = NULL;
    return;
    }
  // vLights.data[lightId].numMeshlets = numMeshlets;
  // return;

  vLights.data[lightId].numMeshlets = meshListPtr;
  pos.data[meshListPtr] = numMeshlets;
  for(uint i=laneID; i<numMeshlets; i+=NumThreads) {
    pos.data[meshListPtr + i + 1] = meshletsId[i];
    }
  }

void main() {
  const LightSource lx = lights[vLights.data[gl_WorkGroupID.x].id];

  traceMeshlets(1, pos.data[0], lx);
  barrier();

  if(numMeshlets>=meshletsId.length()) {
    vLights.data[gl_WorkGroupID.x].numMeshlets = 0;
    return;
    }
  store(gl_WorkGroupID.x);
  }
